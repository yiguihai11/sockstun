plugins {
	id 'com.android.application'
	id 'org.jetbrains.kotlin.android'
}

android {
	namespace "hev.sockstun"
	compileSdkVersion 34
	ndkVersion "26.3.11579264"

	defaultConfig {
		applicationId "hev.sockstun"
		minSdkVersion 26
		targetSdkVersion 34
		versionCode 1
		versionName "1.0"
		setProperty("archivesBaseName", "$applicationId-$versionName")
		externalNativeBuild {
			ndkBuild {
				arguments "APP_CFLAGS+=-DPKGNAME=hev/sockstun -ffile-prefix-map=${rootDir}=."
				arguments "APP_LDFLAGS+=-Wl,--build-id=none"
			}
		}
	}

	buildFeatures {
		compose true
		viewBinding true
	}

	composeOptions {
		kotlinCompilerExtensionVersion = "1.5.10"
	}

	kotlinOptions {
		jvmTarget = "17"
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_17
		targetCompatibility JavaVersion.VERSION_17
	}

	splits {
		abi {
			enable true
			reset()
			include "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
			universalApk false
		}
	}

	signingConfigs {
		release {
		}
	}

	buildTypes {
		release {
			minifyEnabled true
			shrinkResources true
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
			signingConfig signingConfigs.release
		}
		debug {
			minifyEnabled false
			signingConfig signingConfigs.release
			packagingOptions {
				doNotStrip '**/*.so'
			}
		}
	}

	def propsFile = rootProject.file('store.properties')
	def configName = 'release'

	if (propsFile.exists() && android.signingConfigs.hasProperty(configName)) {
		def props = new Properties()
		props.load(new FileInputStream(propsFile))
		if (props!=null && props.containsKey('storeFile')) {
			android.signingConfigs[configName].storeFile = rootProject.file(props['storeFile'])
			android.signingConfigs[configName].storePassword = props['storePassword']
			android.signingConfigs[configName].keyAlias = props['keyAlias']
			android.signingConfigs[configName].keyPassword = props['keyPassword']
		}
	}

	externalNativeBuild {
		ndkBuild {
			path "src/main/jni/Android.mk"
		}
	}

	lintOptions {
		checkReleaseBuilds false
		abortOnError false
	}

	dependenciesInfo {
		includeInApk = false
		includeInBundle = false
	}
}

dependencies {
	implementation 'androidx.appcompat:appcompat:1.4.1'
	implementation 'androidx.core:core-ktx:1.12.0'

	// Jetpack Compose
	def composeBom = platform('androidx.compose:compose-bom:2024.06.00')
	implementation composeBom
	implementation 'androidx.compose.ui:ui'
	implementation 'androidx.compose.ui:ui-graphics'
	implementation 'androidx.compose.ui:ui-tooling-preview'
	implementation 'androidx.compose.material3:material3'
	implementation 'androidx.activity:activity-compose:1.9.0'
	implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.0'
	implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.8.0'

	debugImplementation 'androidx.compose.ui:ui-tooling'
	debugImplementation 'androidx.compose.ui:ui-test-manifest'
}
